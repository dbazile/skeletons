plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}


group               = 'dbazile'
version             = '1.0.0-SNAPSHOT'
mainClassName       = 'dbazile.Main'

sourceCompatibility = 1.9


dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    compile 'log4j:log4j:1.2.17'
//    compile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.19.0'
}


repositories {
    mavenCentral()
}


publishing {
    repositories {
        maven {
            url "https://maven-internal/path/to/${version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = MAVEN_INTERNAL_USERNAME  // from ~/.gradle/gradle.properties
                password = MAVEN_INTERNAL_PASSWORD  // from ~/.gradle/gradle.properties
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
        }
    }
}


// javadoc {
//     doFirst {
//         options.addStringOption('-add-modules', 'java.xml.bind')
//     }
// }


task bundle(type: Jar) {
    manifest.attributes 'Main-Class': mainClassName
    appendix = 'bundle'

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }

    with jar
}


task javadocJar(type: Jar) {
    dependsOn += javadoc

    from javadoc.destinationDir
    classifier 'javadoc'
}
