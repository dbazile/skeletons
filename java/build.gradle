plugins {
    id 'java'
    id 'application'
    id 'maven-publish'

    id 'nebula.release' version '9.2.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
}


group = 'dbazile'

mainClassName = 'dbazile.Main'

sourceCompatibility = 1.9
targetCompatibility = 1.9


dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    compile 'log4j:log4j:1.2.17'

    // JAX-RS + Embedded Jetty
    compile 'org.glassfish.jersey.core:jersey-server:2.27'
    compile 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.27'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.27'
    compile 'org.glassfish.jersey.inject:jersey-hk2:2.27'
    compile 'org.eclipse.jetty:jetty-server:9.4.12.v20180830'

    // JAXB
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'org.glassfish.jaxb:jaxb-runtime:2.3.0.1'
    compile 'javax.activation:activation:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.19.0'
}


repositories {
    mavenCentral()
}


publishing {
    repositories {
        maven {
            url "https://maven-internal/path/to/${version.toString().endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = ''
                password = ''
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }
}


jar {
    manifest.attributes(
        'Implementation-Title':   project.name,
        'Implementation-Version': project.version,
        'Main-Class':             mainClassName,
        'Build-Author':           System.getProperty('user.name'),
        'Build-Host':             InetAddress.localHost.hostName,
        'Build-Timestamp':        new Date(),
        'Git-Commit':             grgit.head().id,
    )
}


/*
 * NOTE: This is just here as a simple reference. As it stands, JAX-RS JSON
 *       serialization doesn't work properly because of path collisions in
 *       META-INF/services/* (and probably a dozen more reasons) but it might
 *       work for applications that don't try too much classpath wizardry.
 */
//task fatjar(type: Jar) {
//    appendix = 'bundle'
//    duplicatesStrategy = DuplicatesStrategy.WARN
//    manifest.attributes = jar.manifest.attributes
//
//    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } {
//        exclude 'META-INF/*.SF'
//        exclude 'META-INF/*.DSA'
//        exclude 'META-INF/*.RSA'
//    }
//
//    with jar
//}


task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}


////////////////////////////////////////////////////////////////////////////////

// Helpers

/*
 * NOTE: This is here as a reference if grgit plugin can't be used for
 *       whatever reason.
 */
//def _getCommit(truncate = Integer.MAX_VALUE) {
//    new ByteArrayOutputStream().with { stream ->
//        exec {
//            commandLine = ['git', 'rev-parse', 'HEAD']
//            standardOutput = stream
//        }
//
//        return stream.toString().trim().take(truncate)
//    }
//}
